module cu_top (
    input clk,                // 100MHz clock
    input rst_n,
    
    input port_enb,
    input port_clk,
    
    inout port_rw,
    inout port_address[16],
    inout port_data[8],
    
    output port_data_rw,
    output port_dmab,
    output port_nmib,
   
    output hsync,
    output vsync,
    output hblank,
    output vblank,
    output rgb[6],
    
    output debug
  ) {
  
  const PALETTE = $reverse({
      6b000000,
      6b000001,
      6b010010,
      6b001001,
      6b100100,
      6b010101,
      6b101010,
      6b111111,
      6b110001,
      6b111000,
      6b111100,
      6b001100,
      6b001011,
      6b100110,
      6b110110,
      6b111110
    });
  
  sig rst;

  .clk(clk) {
    reset_conditioner this_reset_cond;
    edge_detector_with_delay this_start_address_delay(#RISE(0), #FALL(1), #DELAY(20));
    edge_detector_with_delay this_start_data_delay(#RISE(1), #FALL(0), #DELAY(20));
    .rst(rst) {
      vga_signals this_vga_signals;
      dff current_address[14](#INIT(0));
      dff current_data[4](#INIT(0));
      fsm state(#INIT(WAIT)) = {WAIT, WRITE_HIGH_ADDRESS, WRITE_LOW_ADDRESS, WRITE_DATA, WRITE_DATA_HOLD, HOLD};
    }
  }
  
  simple_dual_ram this_vram (.rclk(clk), .wclk(clk), #SIZE(4), #DEPTH(128 * 128));

  always {
    
    // Initialize components
    
    this_reset_cond.in = ~rst_n;
    rst = this_reset_cond.out;
    
    port_rw.enable = 0;
    port_rw.write = 0;
    port_address.enable = 0;
    port_address.write = 0;
    port_data.enable = 0;
    port_data.write = 0;
    
    this_start_address_delay.in = port_clk || port_enb;
    this_start_data_delay.in = port_clk || port_enb;
    
    this_vram.raddr = this_vga_signals.address;
    this_vram.waddr = current_address.q;
    this_vram.write_data = current_data.q;
    this_vram.write_en = 0;
    
    debug = 0;
    
    // Receive commands
    
    case (state.q) {
      state.WAIT:
        if (this_start_address_delay.out && !port_enb) {
          case (port_address.read[0+:8]) {
              8h00:
                if (!port_rw.read) {
                  state.d = state.WRITE_HIGH_ADDRESS;
                }
              8h01:
                if (!port_rw.read) {
                  state.d = state.WRITE_LOW_ADDRESS;
                }
              8h02:
                if (!port_rw.read) {
                  state.d = state.WRITE_DATA;
                }
            }
        }
        
      state.WRITE_HIGH_ADDRESS:
        if (this_start_data_delay.out) {
          if (!port_enb) {
            state.d = state.HOLD;
            current_address.d[7+:7] = port_data.read[0+:7];
          } else {
            state.d = state.WAIT;
          }
        }
        
      state.WRITE_LOW_ADDRESS:
        if (this_start_data_delay.out) {
          if (!port_enb) {
            state.d = state.HOLD;
            current_address.d[0+:7] = port_data.read[0+:7];
          } else {
            state.d = state.WAIT;
          }
        }
        
      state.WRITE_DATA:
        if (this_start_data_delay.out) {
          if (!port_enb) {
            state.d = state.WRITE_DATA_HOLD;
            current_data.d = port_data.read[0+:4];
          } else {
            state.d = state.WAIT;
          }
        }
        
      state.WRITE_DATA_HOLD:
        state.d = state.HOLD;
        this_vram.write_en = 1;
        //current_address.d = current_address.q + 1;
        debug = 1;
        
      state.HOLD:
          if (port_enb) {
            state.d = state.WAIT;
          }
    }
    
    // Output control signals
  
    port_data_rw = (!port_rw.read & 1) | (port_rw.read & 0);
    port_dmab = 1;
    port_nmib = !this_vga_signals.vblank;

    // Output VGA sync signals
    
    hsync = this_vga_signals.hsync;
    vsync = this_vga_signals.vsync;
    hblank = this_vga_signals.hblank;
    vblank = this_vga_signals.vblank;
    
    // Output VGA color signals

    if (!this_vga_signals.hblank && !this_vga_signals.vblank) {
      // rgb = 6 x{ this_vga_signals.address[3] ^ this_vga_signals.address[10] };
      rgb = PALETTE[this_vram.read_data];
    } else {
      rgb = 6b000000;
    }

  }
}