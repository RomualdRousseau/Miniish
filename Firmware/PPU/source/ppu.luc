module ppu (
    input clk,  // 100MHz clock
    input rst,  // reset,
    
    // VGA Signals
    
    input vga_line_clk,
    input vga_is_drawing,
    
    // VRAM
    
    output vram_addr[8],
    output vram_en,
    output vram_data[4],
    
    // Sprites Memory
    
    output sprites_addr[14],
    input sprites_data[4],
    
    // Map Memory
    
    output map_addr[10],
    input map_data[8]
  ) {
  
  sig hscroll[8];
  sig vscroll[8];
  
  .clk(clk) {
    .rst(rst) {
      fsm state = {IDLE, LOOP, READ_TILE, WRITE_PIXEL};
      dff count[8];
      dff haddress[8];
      dff vaddress[8];
    }
    edge_detector line_clk(#RISE(1), #FALL(0));
  }
  
  always {
    
    line_clk.in = vga_line_clk;
    
    vram_addr = c{vaddress.q[0], haddress.q[0+:7]};
    vram_en = 0;
    vram_data = 4bx;
    
    vscroll = vaddress.q + 64;
    hscroll = haddress.q + 64;
    
    map_addr = c{vscroll[3+:5], hscroll[3+:5]};
    
    sprites_addr = c{map_data, vscroll[0+:3], hscroll[0+:3]};
    
    case (state.q) {
      state.IDLE:
        if (line_clk.out) {
          if (!vga_is_drawing) {
            vaddress.d = 8b0;
          }
          else {
            vaddress.d = vaddress.q + 1;
          }
          haddress.d = 8b0;
          count.d = 128;
          state.d = state.LOOP;
        }
      
      state.LOOP:
        if (count.q == 0) {
          state.d = state.IDLE;
        }
        else {
          count.d = count.q - 1;
          state.d = state.READ_TILE;
        }
      
      state.READ_TILE:
        state.d = state.WRITE_PIXEL;
      
      state.WRITE_PIXEL:
        // Chessboard + Cycle through all colors
        // vram_data = (haddress.q[0] ^ vaddress.q[0]) * toffset.q[0+:4];
        vram_en = 1;
        vram_data = sprites_data;
        haddress.d = haddress.q + 1;
        state.d = state.LOOP;
    }
  }
}