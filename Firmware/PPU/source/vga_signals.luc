module vga_signals (
    input clk,        // 25MHz clock
    input rst,        // reset
    
    // Various clocks
    
    output pixel_clk,
    output line_clk,
    
    // VGA Signals
    
    output hsync,
    output vsync,
    output hblank,
    output vblank,
    output address[14]
  ) {
  
  const WIDTH = 128;
  const HEIGHT = 128;
  const HPIXEL_SIZE = 3;
  const VPIXEL_SIZE = 3;
  
  const HVA_T = 640;
  const HFP_T = HVA_T + 16;
  const HSP_T = HFP_T + 96;
  const HBP_T = HSP_T + 48;
  
  const VVA_T = 480;
  const VFP_T = VVA_T + 10;
  const VSP_T = VFP_T + 2;
  const VBP_T = VSP_T + 33;
  
  const HMIN_T = (HVA_T - WIDTH * HPIXEL_SIZE) / 2;
  const HMAX_T =  HMIN_T + WIDTH * HPIXEL_SIZE;
  const VMIN_T = (VVA_T - HEIGHT * VPIXEL_SIZE) / 2;
  const VMAX_T =  VMIN_T + HEIGHT * VPIXEL_SIZE;
  
  .clk(clk) {
    .rst(rst) {
      dff hcounter[12];
      dff vcounter[10];
      //dff address_buffer[14];
    }
  }
  
  sig pcounter[$clog2(HPIXEL_SIZE)];
  sig lcounter[$clog2(VPIXEL_SIZE)];
  
  sig hvisibility;
  sig vvisibility;
  sig lvisibility;
  sig haddress[7];
  sig vaddress[7];
  
  always {
    
    // Default Values
    
    //address = address_buffer.q;
    
    hsync = hcounter.q >= HFP_T && hcounter.q < HSP_T ? 0 : 1;
    vsync = vcounter.q >= VFP_T && vcounter.q  < VSP_T ? 0 : 1;
    
    hvisibility = hcounter.q >= HMIN_T && hcounter.q < HMAX_T ? 1 : 0;
    vvisibility = vcounter.q >= VMIN_T && vcounter.q < VMAX_T ? 1 : 0;
    lvisibility = vcounter.q >= (VMIN_T - VPIXEL_SIZE) && vcounter.q < (VMAX_T - VPIXEL_SIZE) ? 1 : 0;
    
    hblank = ~hvisibility;
    vblank = ~vvisibility;

    pcounter = hcounter.q - (hcounter.q / HPIXEL_SIZE) * HPIXEL_SIZE;
    lcounter = vcounter.q - (vcounter.q / VPIXEL_SIZE) * VPIXEL_SIZE;
    
    pixel_clk = pcounter == 0 ? 1 : 0;
    line_clk =  lvisibility && hcounter.q == 0 && lcounter == 0 ? 1 : 0;
    
    // Update address
    
    haddress = (hcounter.q - HMIN_T) / HPIXEL_SIZE;
    vaddress = (vcounter.q - VMIN_T) / VPIXEL_SIZE;
    
    if (hvisibility && vvisibility) {
      address = c{vaddress, haddress};
    }
    else {
      address = c{7b0000001, 7b0000000};
    }
  }
  
  always {
    
    // Increment hsync counters
    
    if (hcounter.q < HBP_T) {
      hcounter.d = hcounter.q + 1;
    }
    else {
      hcounter.d = 0;
      
      // Increment vsync counters
      
      if (vcounter.q < VBP_T) {
        vcounter.d = vcounter.q + 1;
      }
      else {
        vcounter.d = 0;
      }
    }
  }
}