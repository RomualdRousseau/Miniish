module vga_signals (
    input clk,        // clock
    input rst,        // reset
    output hsync,
    output hblank,
    output vsync,
    output vblank,
    output address[14]
  ) {
  
  const WIDTH = 128;
  const HEIGHT = 128;
  const PIXEL_SIZE = 3;
  
  const HVA_T = 640;
  const HFP_T = HVA_T + 16;
  const HSP_T = HFP_T + 96;
  const HBP_T = HSP_T + 48;
  
  const VVA_T = 480;
  const VFP_T = VVA_T + 10;
  const VSP_T = VFP_T + 2;
  const VBP_T = VSP_T + 33;

  const HMIN = (HVA_T - WIDTH * PIXEL_SIZE) / 2;
  const HMAX =  HMIN + WIDTH * PIXEL_SIZE;
  const VMIN = (VVA_T - HEIGHT * PIXEL_SIZE) / 2;
  const VMAX =  VMIN + HEIGHT * PIXEL_SIZE;
  
  const FREQ_DIV = 4;
  
  var hvisible;
  var vvisible;
  
  .clk(clk) {
    .rst(rst) {
      fsm hstate = {HVA, HFP, HSP, HBP};
      dff hcounter[12];
      fsm vstate = {VVA, VFP, VSP, VBP};
      dff vcounter[10];
    }
  }
  
  always {
    hvisible = hcounter.q >= (HMIN * FREQ_DIV - 1) && hcounter.q < (HMAX * FREQ_DIV - 1);
    vvisible = vcounter.q >= VMIN && vcounter.q < VMAX;
    
    hsync = 1;
    vsync = 0;
    
    hblank = !hvisible;
    vblank = !vvisible;
    
    if (hvisible && vvisible) {
      address[13:7] = (vcounter.q - VMIN) / PIXEL_SIZE;
      address[6:0] = (hcounter.q - (HMIN * FREQ_DIV - 1)) / (PIXEL_SIZE * FREQ_DIV);
    } else {
      address = 0;
    }
    
    hcounter.d = hcounter.q + 1;
    
    case (hstate.q) {
      hstate.HVA:
        if (hcounter.q == HVA_T * FREQ_DIV - 1)
          hstate.d = hstate.HFP;
      
      hstate.HFP:
        if (hcounter.q == HFP_T * FREQ_DIV - 1)
          hstate.d = hstate.HSP;
      
      hstate.HSP:
        hsync = 0;
        if (hcounter.q == HSP_T * FREQ_DIV - 1)
          hstate.d = hstate.HBP;
      
      hstate.HBP:
        if (hcounter.q == HBP_T * FREQ_DIV - 1) {
          hstate.d = hstate.HVA;
          hcounter.d = 0;
          vcounter.d = vcounter.q + 1;
        }
    }
    
    case (vstate.q) {
      vstate.VVA:
        if (vcounter.q == VVA_T)
          vstate.d = vstate.VFP;
      
      vstate.VFP:
        if (vcounter.q == VFP_T)
          vstate.d = vstate.VSP;
      
      vstate.VSP:
        vsync = 1;
        if (vcounter.q == VSP_T)
          vstate.d = vstate.VBP;
      
      vstate.VBP:
        if (vcounter.q == VBP_T) {
          vstate.d = vstate.VVA;
          vcounter.d = 0;
        }
    }
  }
}