module vga_signals (
    input clk,        // 25MHz clock
    input rst,        // reset
    
    // Various clocks
    
    output pixel_clk,
    output line_clk,
    
    // VGA Signals
    
    output hsync,
    output vsync,
    output hblank,
    output vblank,
    output address[14]
  ) {
  
  const WIDTH = 128;
  const HEIGHT = 128;
  const HPIXEL_SIZE = 3;
  const VPIXEL_SIZE = 3;
  
  const HVA_T = 640;
  const HFP_T = HVA_T + 16 - 1;
  const HSP_T = HFP_T + 96 - 1;
  const HBP_T = HSP_T + 48 - 1;
  
  const VVA_T = 480;
  const VFP_T = VVA_T + 10;
  const VSP_T = VFP_T + 2;
  const VBP_T = VSP_T + 33;
  
  const HMIN_T = (HVA_T - WIDTH * HPIXEL_SIZE) / 2;
  const HMAX_T =  HMIN_T + WIDTH * HPIXEL_SIZE;
  const VMIN_T = (VVA_T - HEIGHT * VPIXEL_SIZE) / 2;
  const VMAX_T =  VMIN_T + HEIGHT * VPIXEL_SIZE;
  
  sig hvisibility;
  sig vvisibility;
  sig lvisibility;
  sig haddress[7];
  sig vaddress[7];
  
  .clk(clk) {
    .rst(rst) {
      dff hcounter[12];
      dff vcounter[10];
      dff pcounter[$clog2(HPIXEL_SIZE)];
      dff lcounter[$clog2(VPIXEL_SIZE)];
    }
  }
  
  always {
    
    // Default Values
    
    hvisibility = hcounter.q >= HMIN_T && hcounter.q < HMAX_T ? 1 : 0;
    vvisibility = vcounter.q >= VMIN_T && vcounter.q < VMAX_T ? 1 : 0;
    lvisibility = vcounter.q >= (VMIN_T - VPIXEL_SIZE) && vcounter.q < (VMAX_T - VPIXEL_SIZE) ? 1 : 0;
    
    hsync = hcounter.q >= HFP_T && hcounter.q < HSP_T ? 0 : 1;
    vsync = vcounter.q >= VFP_T && vcounter.q  < VSP_T ? 0 : 1;
    
    hblank = ~hvisibility;
    vblank = ~vvisibility;
    
    pixel_clk = pcounter.q == 0 ? 1 : 0;
    line_clk =  lvisibility && lcounter.q == 0 ? 1 : 0;
    
    // Update address
    
    if (hvisibility && vvisibility) {
      haddress = (hcounter.q - HMIN_T) / HPIXEL_SIZE;
      vaddress = (vcounter.q - VMIN_T) / VPIXEL_SIZE;
    }
    else {
      haddress = 7b0;
      vaddress = 7b0;
    }
    address = c{vaddress, haddress};
  }
  
  always {
    
    // Increment hsync counters
    
    if (hcounter.q < HBP_T) {
      hcounter.d = hcounter.q + 1;
      
      if (pcounter.q < (HPIXEL_SIZE - 1)) {
        pcounter.d = pcounter.q + 1;
      }
      else {
        pcounter.d = 0;
      }
    }
    else {
      hcounter.d = 0;
      pcounter.d = 0;
      
      // Increment vsync counters
      
      if (vcounter.q < VBP_T) {
        vcounter.d = vcounter.q + 1;
        
        if (lcounter.q < (VPIXEL_SIZE - 1)) {
          lcounter.d = lcounter.q + 1;
        }
        else {
          lcounter.d = 0;
        }
      }
      else {
        vcounter.d = 0;
        lcounter.d = 0;
      }
    }
  }
}